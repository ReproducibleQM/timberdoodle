
    model {
    # [,1] is central pop, [,2] is eastern population
    # prior for year 1 Harvest (reverse time, [1,]=2013)
    harvest[1] ~ dnorm(436095,1.822e-10) # mean, 1/sd2 from 2004-2013 data
    harvest[2] ~ dnorm(161459,9.551e-10)
    
    # round harvest estimates for first years
    Harvest[1,1] <- round(harvest[1])
    Harvest[1,2] <- round(harvest[2])
    
    # prior for max hunters in duck stamp years (up to 3-fold higher than max seen)
    max.hunters[1] ~ dunif(1000000,3000000)
    max.hunters[2] ~ dunif(500000,1500000)
    
    # priors for annual variation in true harvest (sigma.H) 
    # and estimated harvest (sd.HIP, sd.DSS)
    for (p in 1:2){
    sigma.H[p] ~ dunif(1,200000)
    tau.H[p] <- pow(sigma.H[p],-2)
    sd.HIP[p] ~ dunif(1,200000)
    prec.HIP[p] <- pow(sd.HIP[p],-2)
    sd.DSS[p] ~ dunif(1,200000)
    prec.DSS[p] <- pow(sd.DSS[p],-2)
    }
    
    # State process (estimate true Harvest in years 2-51)
    for (p in 1:2){
    for (i in 2:yrs){
    eps[i,p] ~ dnorm(0,tau.H[p])
    Harvest[i,p] <- trunc(Harvest[i-1,p] + eps[i,p]) # autoregressive model
    }
    }
    
    # observation process, recoveries and harvest data
    for (p in 1:2){
    for (i in 1:15){
    HIP[i,p] ~ dnorm(Harvest[i,p],prec.HIP[p])} # end HIP years
    for (j in 13:50){
    frac[j,p] <- stamps[j,p]/max.hunters[p]
    DSS[j,p] ~ dnorm(frac[j,p]*Harvest[j,p],prec.DSS[p])} # end DSS yrs
    } # end p loop
    
    # Determining age-sex class proportions (pi's)
    
    # Summarize known wing samples by age and sex
    for (p in 1:2){
    
    pi[1,p] ~ dunif(0.1, 0.8)
    pi[2,p] ~ dunif(0.1, 0.8)
    pi[3,p] <- 1-(pi[1,p]+pi[2,p])
    
    for (t in 1:yrs){                                               # populations (1 eastern, 2 central)
    wings.class[t,,p] ~ dmulti(pi[,p], wings.total[t,p])          # pi is 3 proportions: 1=juvs, 2=males, 3=females    
    
    H[t,1,p] ~ dbin(pi[1,p], Harvest[rev[t],p])
    H[t,2,p] ~ dbin(pi[2,p], Harvest[rev[t],p])
    H[t,3,p] ~ dbin(pi[3,p], Harvest[rev[t],p])
    } #t
    } #p
    } # end jags model
    
